plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply from: rootProject.file('extras.gradle')

group GROUP
version VERSION

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    archiveBaseName.set(project.name)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName.set(project.name)
    archiveClassifier.set('sources')

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName.set(project.name)
    archiveClassifier.set('javadoc')

    from javadoc.destinationDir
}

task jmathAll(type: Jar, dependsOn: tasks.assemble) {
    dependsOn sourcesJar
    dependsOn javadocJar

    archiveFileName.set('jmath-all.jar')
    destinationDirectory.set(rootProject.buildDir)

    from files(subprojects.collect {
        it.sourceSets.main.output
    })
    from files(subprojects.collect {
        it.sourceSets.main.allSource
    })
    from files(subprojects.collect {
        it.javadoc.destinationDir
    })
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'jmath'
                description = 'Math Extention for Java'
                url = 'https://github.com/tomtzook/jmath'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = ' http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/tomtzook/jmath.git'
                    developerConnection = 'scm:git:ssh://github.com/tomtzook/jmath.git'
                    url = 'https://github.com/tomtzook/jmath'
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseBuild()) {
                name = 'NexusStaging'
                url = NEXUS_RELEASE_REPOSITORY_URL
            } else {
                name = 'NexusSnapshot'
                url = NEXUS_SNAPSHOT_REPOSITORY_URL
            }

            credentials {
                username getNexusUsername()
                password getNexusPassword()
            }
        }
    }
}

if (shouldSign()) {
    signing {
        sign publishing.publications.mavenJava
    }
}
